prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix gg: <http://www.gemeentegeschiedenis.nl/gg-schema#>
prefix graph: <https://iisg.amsterdam/graph/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
select ?wkt ?wktColor ?wktLabel {
  {
    select ?place ?municipality (count(?item) as ?n) {
      graph graph:collection {
        ?item schema:about ?place
      }
      ?place owl:sameAs ?municipality .
      ?municipality a gg:Municipality .
    }
    group by ?place ?municipality
  }
  ?municipality geo:hasGeometry ?geometry1 ;
                rdfs:label ?label .
  ?geometry1 gg:year ?year1 ;
             geo:asWKT ?wkt .
  filter not exists {
    ?municipality geo:hasGeometry ?geometry2 .
    ?geometry2 gg:year ?year2 .
    filter (?year2 > ?year1)
  }
  bind (concat('jet,',?n / 23583) as ?wktColor)
  bind (concat('<h4>',?label,'</h4><p>',str(?n),'</p>') as ?wktLabel)
}
