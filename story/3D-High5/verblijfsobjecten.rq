prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?shape ?shapeLabel ?shapeZ {
  bind(-42.67 as ?shapeZ)
  {
    select ?verblijfsobject ?shape {
      graph graph:3d-bim {
        ?verblijfsobject geo:hasGeometry/geo:asWKT ?shape .
      }
    }
  }
  service <https://data.pdok.nl/sparql> {
    ?verblijfsobject bag:oppervlakte ?oppervlakte ;
                     bag:status ?verblijfsobjectStatus .
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel .
  }
  bind(concat(
    '<h2>Oppervlakte: ',str(?oppervlakte),'mÂ²</h2>',
    '<h2>Status: ',str(?verblijfsobjectStatusLabel),'</h2>',
    '<p><a href="',str(?verblijfsobject),'">link</a></p>') as ?shapeLabel)
  bind(substr(str(?verblijfsobject), 65) as ?shapeName)
}
