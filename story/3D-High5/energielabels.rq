prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix begrip: <http://bgt.basisregistraties.overheid.nl/id/begrip/>
prefix bgt: <http://bgt.basisregistraties.overheid.nl/def/bgt#>
prefix energie: <http://data.labs.pdok.nl/dataset/energie#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
select ?shape ?shapeColor ?shapeHeight (sample(?shapeLabel) as ?shapeLabel) ?shapeName {
  graph graph:3d-bag {
    ?openbareRuimte bag:naamOpenbareRuimte ?openbareRuimteNaam ;
                    bag:bijbehorendeWoonplaats ?woonplaats .
    ?woonplaats bag:naamWoonplaats ?woonplaatsNaam .
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                      bag:huisnummer ?huisnummer ;
                      bag:postcode ?postcode .
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                     bag:oppervlakte ?oppervlakte ;
                     bag:pandrelatering ?pand ;
                     bag:status ?verblijfsobjectStatus .
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel .
    ?pand geo:hasGeometry/geo:asWKT ?shape ;
          bag:oorspronkelijkBouwjaar ?bouwjaar ;
          bag:status ?pandStatus .
    ?pandStatus rdfs:label ?pandStatusLabel .
  }
  ?pand schema:height ?shapeHeight .
  optional {
    ?x energie:ep ?ep ;
       energie:e_label ?energielabel ;
       energie:opname_datum ?opnamedatum ;
       energie:pand_huisnummer ?huisnummer0 ;
       energie:pand_postcode ?postcode ;
       energie:registratie_datum ?registratiedatum .
  }
  filter(str(?huisnummer) = ?huisnummer0)
  bind(
    if(!(bound(?energielabel)), "slateblue",
      if(strstarts(?energielabel, "A"), "#22b14c",
        if(?energielabel="B", "#8ff334",
          if(?energielabel="C", "#bdfc2c",
            if(?energielabel="D", "#fff200",
              if(?energielabel="E", "#ff9a35",
                if(?energielabel="F", "#ff7f27",
                  if(?energielabel="G", "#ed1c24",
                    "grey")))))))) as ?shapeColor)
  bind(if(
    bound(?x),
    concat('<svg height="20" viewBox="0 0 20 20" xmlns="http://www.w3.org/2000/svg"><circle cx="10" cy="10" fill="',str(?shapeColor),'" r="10"/></svg> '),
    '') as ?prefix)
  bind(if(
    bound(?x),
    concat(
      '<h5>Energielabel informatie:</h5>',
      '<ul><li>Energielabel: ',str(?energielabel),'</li>',
      '<li>EP: ',str(?ep),'</li>',
      '<li>Opnamedatum: ',str(?opnamedatum),'</li>',
      '<li>Registratiedatum: ',str(?registratiedatum),'</li></ul><h5>'),
    '') as ?label)
  bind(concat(
    str(?openbareRuimteNaam),' ',str(?huisnummer),', ', str(?postcode),' ',
    str(?woonplaatsNaam)) as ?shapeName)
  bind(concat(
    '<h4>',str(?prefix),?shapeName,'</h4>',
    str(?label),'<a href="',str(?verblijfsobject),'">Verblijfsobject</a>:</h5>',
    '<ul><li>Woonplaats: <a href="',str(?woonplaats),'">',str(?woonplaatsNaam),'</a></li>',
    '<li>Oppervlakte: ',str(?oppervlakte),'mÂ²</li>',
    '<li>Status: <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a></li></ul>',
    '<h5><a href="',str(?pand),'">Pand</a>:</h5>',
    '<ul><li>Bouwjaar: ',str(?bouwjaar),'</li>',
    '<li>Status: <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></li></ul>') as ?shapeLabel)
}
group by ?shape ?shapeColor ?shapeHeight ?shapeName ?shapeZ
limit 100
