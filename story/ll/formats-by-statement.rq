prefix ll: <https://lodlaundromat.org/def/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select
  ?format
  (replace(?statements,'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.') as ?statements)
{
  {
    select ?format (sum(?triples+?quadruples) as ?statements) {
      ?document
        ll:quadruples ?quadruples;
        ll:serializationFormat ?format0;
        ll:triples ?triples.
      # Exclude documents that are standards-compliant but empty.
      filter(?triples > 0 || ?quadruples > 0)
      # Correct for the fact that we do not know ahead of time whether
      # or not something is a triples-only format.
      bind(if(?format0="application/trig"^^xsd:string && ?quadruples=0,
              'text/turtle',
              if(?format0="application/n-quads"^^xsd:string && ?quadruples=0,
                 'application/n-triples',
                 ?format0))
           as ?format)
    }
    group by ?format
    order by desc(?statements)
  }
}
