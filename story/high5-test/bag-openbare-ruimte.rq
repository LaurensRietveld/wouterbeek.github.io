prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?pand ?pandShape ?pandShapeColor ?pandShapeHeight ?pandShapeName ?pandShapeLabel {
  graph ?g1 {
    ?nummeraanduiding1 bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
                       bag:huisnummer 11;
                       bag:postcode "9405VJ".
  }
  filter not exists { ?g1 bag:eindGeldigheid ?eind1. }
  graph ?g2 {
    ?nummeraanduiding2 bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
                       bag:huisnummer ?huisnummer2;
                       bag:postcode ?postcode2.
  }
  filter not exists { ?g2 bag:eindGeldigheid ?eind2. }
  graph ?g3 {
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding2;
                     bag:pandrelatering ?pand.
  }
  filter not exists { ?g3 bag:eindGeldigheid ?eind3. }
  graph ?g4 {
    ?pand geo:hasGeometry/geo:asWKT ?pandShape;
          bag:identificatiecode ?pandId;
          bag:status ?pandStatus;
          bag:oorspronkelijkBouwjaar ?bouwjaar.
  }
  filter not exists { ?g4 bag:eindGeldigheid ?eind4. }
  ?woonplaats rdfs:label ?woonplaatsNaam.
  graph ?g5 {
    ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats;
                    bag:naamOpenbareRuimte ?openbareRuimteNaam.
  }
  filter not exists { ?g5 bag:eindGeldigheid ?eind5. }
  bind(if(?postcode2="9405VJ" && ?huisnummer2=11,'red','blue') as ?pandShapeColor)
  bind(30 as ?pandShapeHeight)
  bind(concat(
    str(?openbareRuimteNaam),' ',str(?huisnummer2),', ',
    str(?postcode2),' ',str(?woonplaatsNaam)) as ?pandShapeName)
  bind(concat('<h2>',?pandShapeName,'</h2>') as ?pandShapeLabel)
}
limit 100
