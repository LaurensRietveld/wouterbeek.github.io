prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select
  ?bouwjaar
  ?huisnummer
  ?nummeraanduidingId
  ?openbareRuimteId
  ?openbareRuimteNaam
  ?openbareRuimteStatusNaam
  ?oppervlakte
  ?pandId
  ?pandStatusNaam
  ?postcode
  ?pandShape
  ?pandShapeHeight
  ?verblijfsobjectId
  ?verblijfsobjectStatusNaam
  ?woonplaatsNaam
{
  bind(11 as ?huisnummer)
  bind("9405VJ"^^xsd:string as ?postcode)
  service <https://data.pdok.nl/sparql> {
    ?woonplaats rdfs:label ?woonplaatsNaam.
    graph ?g1 {
      ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats;
                      bag:identificatiecode ?openbareRuimteId;
                      bag:naamOpenbareRuimte ?openbareRuimteNaam;
                      bag:status ?openbareRuimteStatus.
    }
    ?openbareRuimteStatus rdfs:label ?openbareRuimteStatusNaam.
    filter not exists { ?g1 bag:eindGeldigheid ?eind1. }
    graph ?g2 {
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
                        bag:huisnummer ?huisnummer;
                        bag:identificatiecode ?nummeraanduidingId;
                        bag:postcode ?postcode.
    }
    filter not exists { ?nummeraanduiding bag:huisletter ?letter. }
    filter not exists { ?g2 bag:eindGeldigheid ?eind2. }
    graph ?g3 {
      ?verblijfsobject bag:hoofdadres ?nummeraanduiding;
                       bag:identificatiecode ?verblijfsobjectId;
                       bag:oppervlakte ?oppervlakte;
                       bag:pandrelatering ?pand;
                       bag:status ?verblijfsobjectStatus.
    }
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusNaam.
    filter not exists { ?g3 bag:eindGeldigheid ?eind3. }
    graph ?g4 {
      ?pand geo:hasGeometry/geo:asWKT ?pandShape;
            bag:identificatiecode ?pandId;
            bag:status ?pandStatus;
            bag:oorspronkelijkBouwjaar ?bouwjaar.
    }
    ?pandStatus rdfs:label ?pandStatusNaam.
    filter not exists { ?g4 bag:eindGeldigheid ?eind4. }
  }
  bind(30 as ?pandShapeHeight)
}
limit 5
