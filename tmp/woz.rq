prefix cbs: <https://triply.cc/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://triply.cc/cbs/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeColor ?shapeLabel {
  bind("Amsterdam"^^xsd:string as ?name1)
  {
    select (min(?woz) as ?min) (max(?woz) as ?max) {
      graph graph:wijkbuurt-2016 {
        ?feature3
          cbs:woz ?avgValue;
          geo:sfWithin/geo:sfWithin/rdfs:label ?name1.
      }
    }
  }
  graph graph:wijkbuurt-2016 {
    ?feature3
      cbs:woz ?avgValue;
      geo:hasGeometry/geo:asWKT ?shape;
      geo:sfWithin [ geo:sfWithin/rdfs:label ?name1; rdfs:label ?name2 ];
      rdfs:label ?name3.
  }
  bind((xsd:float(?avgValue)-xsd:float(?min))/(xsd:float(?max)-xsd:float(?min)) as ?normalized)
  bind(concat('jet,',str(?normalized)) as ?shapeColor)
  bind(concat(
    '<h3>',str(?name1),' »<br>',str(?name2),' »<br>',str(?name3),'</h3>',
    '<h4>Average house value: € ',replace((1000*?avgValue),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),',-</h4>') as ?shapeLabel)
}
order by desc(?avgValue)
limit 100
